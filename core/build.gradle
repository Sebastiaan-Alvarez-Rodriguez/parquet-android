/**
 * Goal of this module: Add parquet-floor to the merged parquet-column and parquet-hadoop,
 * We also add a pom file with original parquet-floor dependencies,
 * excluding parquet-column and parquet-hadoop.
 */


buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id 'java-library'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group our_group
version our_version

// This task adds jdk internal module to this library, so we can access its 'Unsafe' class.
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs = ['--add-exports=java.base/jdk.internal.misc=ALL-UNNAMED']
}

configurations {
    doc {
        transitive true
    }
}

dependencies {
    api("blue.strategic.parquet:parquet-floor:$parquet_floor_version") {
        transitive = false
    }
    implementation project(path: ':parquet-merged', configuration: 'shadow')
    implementation "org.xerial.snappy:snappy-java:1.1.8.3"
    doc "org.apache.parquet:parquet-hadoop:$parquet_version"
    doc "org.apache.parquet:parquet-column:$parquet_version"
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    dependsOn subprojects.javadoc
    source subprojects.javadoc.source
    source configurations.doc.collect { zipTree(it) }
    source sourceSets.main.allJava
    classpath = sourceSets.main.output + sourceSets.main.compileClasspath
    include '**/*.java'
    options.addStringOption('Xdoclint:none', '-quiet')
}

jar {}

shadowJar {
//    dependencies {
//        exclude(dependency("org.xerial.snappy:snappy-java:1.1.8.3"))
//    }
}

