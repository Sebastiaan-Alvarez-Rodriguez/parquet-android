/**
 * Goal of this module: Override a few classes from parquet-floor, and parquet,
 * to improve its capabilities.
 * Fat-jar has no duplicate files inside.
 */

plugins {
    id 'java-library'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group our_group
version our_version

configurations {
    doc.extendsFrom(shadow)
    testImplementation.extendsFrom(shadow)
}

dependencies {
    api("com.github.strategicblue:parquet-floor:$parquet_floor_version") {
        transitive = false
    }

    implementation("org.apache.parquet:parquet-hadoop:$parquet_version") {
        transitive = false
    }
    implementation("org.apache.parquet:parquet-column:$parquet_version") {
        transitive = false
    }

    compileOnly "org.apache.parquet:parquet-common:$parquet_version"
    compileOnly "org.apache.parquet:parquet-encoding:$parquet_version"
    compileOnly "org.apache.parquet:parquet-format-structures:$parquet_version"
    compileOnly "org.apache.parquet:parquet-jackson:$parquet_version"
    compileOnly "org.apache.yetus:audience-annotations:$parquet_yetus_version"
    compileOnly "org.slf4j:slf4j-api:$parquet_slf4j_version"
}

java {
    withJavadocJar()
    withSourcesJar()

    sourceCompatibility = android_java_version_src
    targetCompatibility = android_java_version_target
}

javadoc {
    failOnError false

    source configurations.doc.collect { zipTree(it) }
    include '**/*.java'
    include '**/*.scala'
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

jar {}

shadowJar {
    archiveClassifier.set('')
}