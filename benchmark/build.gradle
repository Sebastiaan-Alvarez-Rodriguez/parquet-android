plugins {
    id 'com.android.library'
    id 'androidx.benchmark'
}

/**
 * Test execution is handled by gradle.
 * androidx.benchmark.junit4.AndroidBenchmarkRunner delegates work to android junit runner, and accepts the same arguments.
 * See the potential arguments here:
 *   - https://github.com/android/android-test/blob/androidx-test-1.1/runner/android_junit_runner/java/androidx/test/runner/AndroidJUnitRunner.java#L76
 *   - (deprecated link, still provides correct args): https://developer.android.com/reference/android/support/test/runner/AndroidJUnitRunner.html#execution-options:
 *
 * We list the benchmark commands we used for testing below.
 *
 * > Note that Output data (json file) can be found on the android device in:
 * > storage/self/primary/Android/media/org.sebastiaan.parquet_android.benchmark.test.org.sebastiaan.parquet_android.benchmark.test-benchmarkData.json
 *
 * ## All tests at once
 * gradle benchmark:connectedCheck -P android.testInstrumentationRunnerArguments.tests_regex=org.sebastiaan.parquet_android.*
 *
 * ## Parquet tests - individual
 * gradle benchmark:connectedCheck -P android.testInstrumentationRunnerArguments.class=org.sebastiaan.parquet_android.ParquetBenchmark#parquetWriteUncompressed
 * gradle benchmark:connectedCheck -P android.testInstrumentationRunnerArguments.class=org.sebastiaan.parquet_android.ParquetBenchmark#parquetWriteSnappy
 *
 * gradle benchmark:connectedCheck -P android.testInstrumentationRunnerArguments.class=org.sebastiaan.parquet_android.ParquetBenchmark#parquetReadUncompressed
 * gradle benchmark:connectedCheck -P android.testInstrumentationRunnerArguments.class=org.sebastiaan.parquet_android.ParquetBenchmark#parquetReadSnappy
 *
 * ## CSV tests - individual
 * gradle benchmark:connectedCheck -P android.testInstrumentationRunnerArguments.class=org.sebastiaan.parquet_android.CSVBenchmark#csvWrite
 * gradle benchmark:connectedCheck -P android.testInstrumentationRunnerArguments.class=org.sebastiaan.parquet_android.CSVBenchmark#csvRead
 */
android {
    compileSdk 32

    defaultConfig {
        minSdk 29
        targetSdk 32

        // Set this argument to capture profiling information, instead of measuring performance.
        // Can be one of:
        //   * None
        //   * StackSampling
        //   * MethodTracing
        // See full descriptions of available options at: d.android.com/benchmark#profiling
        testInstrumentationRunnerArguments["androidx.benchmark.profiling.mode"] = 'None'

        testInstrumentationRunner 'androidx.benchmark.junit4.AndroidBenchmarkRunner'
        consumerProguardFiles "consumer-rules.pro"
    }

    testBuildType = "release"

    buildTypes {
        release {
            minifyEnabled true // very important, otherwise all source code is removed (obviously)
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // The androidx.benchmark plugin configures release buildType with proper settings, such as:
            // - disables code coverage
            // - adds CPU clock locking task
            // - signs release buildType with debug signing config
            // - copies benchmark results into build/outputs/connected_android_test_additional_output folder
        }
    }

    compileOptions {
        sourceCompatibility android_java_version_src
        targetCompatibility android_java_version_target
    }
}

configurations {
    implementation.extendsFrom(embed)
    testImplementation.extendsFrom(implementation)
    androidTestImplementation.extendsFrom(testImplementation)
    androidTestCompileOnly.extendsFrom(testCompileOnly)
}

dependencies {
    implementation project(path: ':parquet-android')
    implementation 'com.opencsv:opencsv:5.6'
    // parquet-floor-android dependencies below repeated for testing
    testCompileOnly project(path: ':parquet-android:parquet-floor-android')
    testCompileOnly("com.github.strategicblue:parquet-floor:$parquet_floor_version") {
        transitive = false
    }
    testCompileOnly("org.apache.parquet:parquet-hadoop:$parquet_version") {
        transitive = false
    }
    testCompileOnly("org.apache.parquet:parquet-column:$parquet_version") {
        transitive = false
    }

    implementation "org.apache.parquet:parquet-common:$parquet_version"
    compileOnly "org.apache.parquet:parquet-encoding:$parquet_version"
    compileOnly "org.apache.parquet:parquet-format-structures:$parquet_version"
    compileOnly "org.apache.parquet:parquet-jackson:$parquet_version"
    compileOnly "org.apache.yetus:audience-annotations:$parquet_yetus_version"
    compileOnly "org.slf4j:slf4j-api:$parquet_slf4j_version"

    // Test dependencies
    implementation "androidx.benchmark:benchmark-common:$benchmark_version"
    implementation "androidx.benchmark:benchmark-junit4:$benchmark_version"

    testImplementation "junit:junit:$benchmark_junit_version"

    testImplementation "androidx.test:runner:$benchmark_androidxtest_version"
    testImplementation "androidx.test:rules:$benchmark_androidxtest_version"
    testImplementation "androidx.test.ext:junit:$benchmark_testext_version"

    testImplementation project(path: ':testutils')
//    testCompileOnly project(path: ':parquet-android:parquet-floor-android')
//
//
//    // Android Test dependencies
//    androidTestCompileOnly project(path: ':parquet-android:parquet-floor-android')
}

java {
    sourceCompatibility = android_java_version_src
    targetCompatibility = android_java_version_target
}